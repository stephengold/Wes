// Gradle script to build and run the Wes project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    jmeTarget = '' // distinguish non-JME libraries built for specific JME releases
    wesSnapshot = '-SNAPSHOT' // for development builds
    //wesSnapshot = '' // for release builds
    wesVersion = '0.8.3' + jmeTarget + wesSnapshot
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

tasks.register('AppChooser') {
    dependsOn ':WesExamples:AppChooser'
    description = 'Runs the example apps.'
}

tasks.register('checkstyle') {
    dependsOn ':WesExamples:checkstyleMain', ':WesLibrary:checkstyleMain'
    description = 'Checks the style of all Java sourcecode.'
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn ':WesLibrary:install'
    description = 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':WesLibrary:release'
    description = 'Stages Maven artifacts to the Central Publisher Portal.'
}
