// Gradle build script for the Wes project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    wesSnapshot = '-SNAPSHOT' // for development builds
    //wesSnapshot = '' // for release builds

    jmeTarget = '' // distinguish non-JME libraries built for specific JME releases

    // module coordinates of external dependencies:
    heartCoordinates = 'com.github.stephengold:Heart:9.0.0' + jmeTarget

    // current versions of libraries:
    jme3Version = '3.6.1-stable'
    wesVersion = '0.8.1' + jmeTarget + wesSnapshot
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

tasks.register('checkstyle') {
    dependsOn ':WesExamples:checkstyleMain', ':WesLibrary:checkstyleMain'
    description 'Checks the style of all Java sourcecode.'
}

tasks.register('AppChooser') {
    dependsOn ':WesExamples:AppChooser'
    description 'Runs the example apps.'
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn ':WesLibrary:install'
    description 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':WesLibrary:release'
    description 'Stages Maven artifacts to Sonatype OSSRH.'
}
