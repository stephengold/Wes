// Gradle script to build and run the WesExamples subproject of Wes

// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application' // to build JVM applications
}

dependencies {
    implementation(libs.commons.exec)

    implementation(libs.heart)
    implementation(libs.acorus)
    runtimeOnly(libs.jme3.desktop)
    implementation(libs.jme3.lwjgl3)
    implementation(libs.jme3.plugins)
    runtimeOnly(libs.jme3.testdata)

    // WesExamples doesn't use jme3-jogg
    //  -- it is included solely to avoid runtime warnings from AssetConfig:
    runtimeOnly(libs.jme3.jogg)

    //implementation 'com.github.stephengold:Wes' + wesVersion // for published library
    implementation project(':WesLibrary') // for local library build
}

// Register cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSandbox', 'cleanSOs')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanLogs', Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
tasks.register('cleanSandbox', Delete) { // Acorus sandbox
    delete 'Written Assets'
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}

// Register tasks to run the example apps:

tasks.register('AppChooser', JavaExec) {
    mainClass = 'jme3utilities.test.wes.AppChooser'
}

tasks.register('ConvertToInPlace', JavaExec) {
    mainClass = 'jme3utilities.test.wes.ConvertToInPlace'
}
tasks.register('FlashMobDemo', JavaExec) {
    mainClass = 'jme3utilities.test.wes.FlashMobDemo'
}
tasks.register('FlashMobDemo-deleteSettings', JavaExec) {
    args '--deleteOnly'
    mainClass = 'jme3utilities.test.wes.FlashMobDemo'
}
tasks.register('ReverseAnimation', JavaExec) {
    mainClass = 'jme3utilities.test.wes.ReverseAnimation'
}
tasks.register('TrimAnimation', JavaExec) {
    mainClass = 'jme3utilities.test.wes.TrimAnimation'
}
