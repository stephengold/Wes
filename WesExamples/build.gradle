// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application'
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'jme3utilities.test.wes.AppChooser'
}
jar.manifest.attributes('Main-Class': 'jme3utilities.test.wes.AppChooser')

dependencies {
    implementation 'org.apache.commons:commons-exec:1.3'

    implementation heartCoordinates
    implementation 'com.github.stephengold:Acorus:0.9.16'
    runtimeOnly 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-plugins:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:' + jme3Version

    // WesExamples doesn't use jme3-jogg
    //  -- it is included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version

    //implementation 'com.github.stephengold:Wes' + wesVersion // for published library
    implementation project(':WesLibrary') // for latest sourcecode
}

// cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSandbox', 'cleanSOs')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanLogs', Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
tasks.register('cleanSandbox', Delete) { // Acorus sandbox
    delete 'Written Assets'
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}

// tasks to run the example apps:

tasks.register('AppChooser', JavaExec) {
    mainClass = 'jme3utilities.test.wes.AppChooser'
}

tasks.register('ConvertToInPlace', JavaExec) {
    mainClass = 'jme3utilities.test.wes.ConvertToInPlace'
}
tasks.register('FlashMobDemo', JavaExec) {
    mainClass = 'jme3utilities.test.wes.FlashMobDemo'
}
tasks.register('FlashMobDemo-deleteSettings', JavaExec) {
    args '--deleteOnly'
    mainClass = 'jme3utilities.test.wes.FlashMobDemo'
}
tasks.register('ReverseAnimation', JavaExec) {
    mainClass = 'jme3utilities.test.wes.ReverseAnimation'
}
tasks.register('TrimAnimation', JavaExec) {
    mainClass = 'jme3utilities.test.wes.TrimAnimation'
}
