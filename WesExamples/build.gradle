// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

apply plugin: 'application'

description = 'demos, examples, and tests that do not generate reports'

mainClassName = 'jme3utilities.test.wes.FlashMobDemo'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

dependencies {
    implementation 'com.github.stephengold:jme3-utilities-ui:0.9.2'
    implementation 'org.jmonkeyengine:jme3-desktop:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-plugins:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:3.1.0-stable'

    // WesExamples doesn't use jme3-blender or jme3-jogg
    //  -- they are included solely to avoid warnings from AssetConfig
    runtimeOnly 'org.jmonkeyengine:jme3-blender:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jmonkeyengineVersion

    //implementation "jme3utilities:Wes:$wesVersion"
    implementation project(':WesLibrary')
}

// cleanup tasks
clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs', 'cleanWrittenAssets')

task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) {
    delete 'Written Assets'
}

// run the example apps
task runFlashMobDemo(type: JavaExec) {
    main 'jme3utilities.test.wes.FlashMobDemo'
}
task runReverseAnimation(type: JavaExec) {
    main 'jme3utilities.test.wes.ReverseAnimation'
}
task runTrimAnimation(type: JavaExec) {
    main 'jme3utilities.test.wes.TrimAnimation'
}
